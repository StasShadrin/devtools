plugins {
    id 'java'
    id 'application'
    id 'checkstyle'
    id 'jacoco'
}

group = 'ru.mentee.power'
version = '1.0-SNAPSHOT'

ext {
    junitVersion = '5.10.0'
    assertjVersion = '3.25.3'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(25)
    }
}

application {
    mainClass = 'ru.mentee.power.devtools.progress.ProgressTracker'
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation platform("org.junit:junit-bom:${junitVersion}")
    testImplementation "org.junit.jupiter:junit-jupiter"
    testImplementation "org.assertj:assertj-core:${assertjVersion}"
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

checkstyle {
    toolVersion = '10.12.4'
    maxErrors = 0
    maxWarnings = 999
    configFile = file("${rootDir}/config/checkstyle/checkstyle.xml")
}

jacoco {
    toolVersion = "0.8.14"
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport

    filter {
        excludeTestsMatching '*Demo*'
        excludeTestsMatching '*ProgressDemo*'
    }
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = false
        csv.required = false
        html.required = true
        html.outputLocation = layout.buildDirectory.dir('reports/jacoco/test/html')
    }

    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                    'ru/mentee/power/devtools/progress/ProgressDemo.class',
                    'ru/mentee/power/devtools/progress/**/ProgressDemo*.class',
                    '**/*Demo*.class'
            ])
        }))
    }
}

jacocoTestCoverageVerification {
    dependsOn jacocoTestReport

    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                    'ru/mentee/power/devtools/progress/ProgressDemo.class',
                    'ru/mentee/power/devtools/progress/**/ProgressDemo*.class',
                    '**/*Demo*.class'
            ])
        }))
    }

    violationRules {
        rule {
            limit {
                minimum = 0.80
            }
        }
    }
}

// Интеграция проверок в lifecycle
check {
    dependsOn jacocoTestCoverageVerification
}

run {
    standardInput = System.in
    systemProperty "file.encoding", "UTF-8"
}